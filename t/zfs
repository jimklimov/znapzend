#!/usr/bin/env perl

use strict;
use warnings;

use FindBin;
use Time::Piece;

my %dataSets = (
    'tank'               => '',
    'tank/source'        => 'src',
    'tank/source/child'        => 'src-inherited', # Should "list" but not "get -s local"; should also "get -s (local,)inherited" though
    'tank/source/child/grandchild'        => 'src-inherited',
    'tank/anothersource'        => 'src',
    'tank/anothersource/anotherchild'        => 'src-inherited',
    'tank/anothersource/anotherchild/anewconfig'        => 'src',
    'tank/anothersource/anotherchild/anewconfig/newcfgdata'        => 'src-inherited',
    'tank/anothersource/disabled'         => 'src-disabled',
    'tank/anothersource/disabled/also'    => 'src-disabled',
    'backup'             => '',
    'backup/destination' => 'dst',
    'backup/destination/child' => 'dst', # TODO: dst-inherited
    'backup/destination/child/grandchild' => 'dst',
    'backup/destination/disabled' => 'dst-disabled',
    'remote'             => '',
    'remote/destination' => 'dst',
    'remote/destination/child' => 'dst',
    'remote/destination/child/grandchild' => 'dst',
);

# List explicitly so we know the `zfs` CLI argument is a
# dataset name from tests, but points to failure codepath
my %dataSetsMissing = (
    'missingpool'        => 'fail'
);

sub find_attr_source {
    my $srcname = shift;
    my $attr = shift; # only matters for "enabled" at the moment

    if (!defined($srcname) || $srcname eq '') {
        die "FATAL: zfs mock find_attr_source() got empty/undef srcame";
    }

    if (!defined($dataSets{"$srcname"})) {
        die "FATAL: zfs mock find_attr_source() got unlisted srcame '$srcname' (not in dataSets table)";
        # return undef;
    }

    if ($dataSets{"$srcname"} eq 'src') {
        return 'local';
    }

    if ($dataSets{"$srcname"} eq '') {
        return 'default'; # Should not really be seen for our unconfigured datasets like pool roots etc
    }

    if (($dataSets{"$srcname"} eq 'src-disabled') && $attr eq 'enabled') {
        return 'local';
    }

    if ($dataSets{"$srcname"} eq 'dst' || $dataSets{"$srcname"} eq 'dst-disabled') {
        return 'received';
    }

    if ( $dataSets{"$srcname"} =~ /-inherited$/ ) {
        my $parentSourceName = $srcname;
        $parentSourceName =~ s,/[^/]+$,, ;
        if (!defined($parentSourceName) || $parentSourceName eq '') {
            die "FATAL: zfs mock find_attr_source() got empty/undef parentSourceName";
        }
        if ($parentSourceName eq $srcname) {
            die "FATAL: zfs mock find_attr_source() got same parentSourceName as srcname '$srcname'";
        }

        my $parentSource = find_attr_source($parentSourceName);
        if (!defined($parentSource) || $parentSource eq '-' || $parentSource eq 'default') {
            return 'default'; #...or shoul we return $parentSource here?
        }

        if ($parentSource eq 'local' || $parentSource eq 'received') {
            return "inherited from $parentSourceName";
        }

        return $parentSource;
    }

    return '-';
}

sub print_zfs_get {
    # Arguments are all optional; defaults are matched for the legacy tests
    my $prefix = shift; # Dataset name to prepend (and add a whitespace)
    my $srcname = shift;
    my $dstname = shift;
    my $printsrc = shift;

    if ( defined($prefix) ) {
        chomp $prefix;
        if (!defined($srcname) || $srcname eq '') { $srcname = $prefix; }
    } else {
        $prefix = "";
        if (!defined($srcname) || $srcname eq '') { $srcname = 'tank/source'; }
    }

    # Note: for DST we only use the pool-less part,
    # its prefix differs for local and remote.
    # More so for the legacy auto-guesswork codepath.
    if (!defined($dstname) || $dstname eq '') {
        if ($prefix eq 'tank/source' || $prefix eq '') {
            $dstname = 'destination';
        } else {
            $dstname = 'destination/' . $prefix;
        }
    }

    if ($prefix ne '') { $prefix .= " "; }

# print STDERR "=== zfs get: '$prefix' '$srcname' '$dstname'\n";

    my $srcenabled = "on";
    if (defined($dataSets{"$srcname"}) && ($dataSets{"$srcname"} eq 'src-disabled')) {
        $srcenabled = "off";
    }

    my $srcsuffix = "";
    my $srcenabledsuffix = "";
    if ($printsrc) {
        $srcsuffix = "\t" . find_attr_source($srcname, '');
        $srcenabledsuffix = "\t" . find_attr_source($srcname, 'enabled');
    }

    my $dst0precmd = "/bin/echo 'pre znap command for dst_0'";
    my $dst0pstcmd = "/bin/echo 'post znap command for dst_0'";
    if ( defined($ENV{'ZNAPZENDTEST_ZFS_GET_DST0PRECMD_FAIL'}) ) {
        $dst0precmd = "/bin/false";
    }
    if ( defined($ENV{'ZNAPZENDTEST_ZFS_GET_DST0PSTCMD_FAIL'}) ) {
        $dst0pstcmd = "/bin/false";
    }

    print <<"ZFS_GET_END";
${prefix}org.znapzend:dst_0_plan     1hours=>10minutes,30minutes=>5minutes,10minutes=>60seconds${srcsuffix}
${prefix}org.znapzend:dst_0_precmd   ${dst0precmd}${srcsuffix}
${prefix}org.znapzend:dst_0_pstcmd   ${dst0pstcmd}${srcsuffix}
${prefix}org.znapzend:dst_a_plan     1hours=>10minutes,40minutes=>5minutes,20minutes=>60seconds${srcsuffix}
${prefix}org.znapzend:dst_fail_plan  1hours=>1minutes${srcsuffix}
${prefix}org.znapzend:src            ${srcname}${srcsuffix}
${prefix}org.znapzend:src_plan       1hours=>10minutes,30minutes=>5minutes,1minutes=>6seconds${srcsuffix}
${prefix}org.znapzend:recursive      on${srcsuffix}
${prefix}org.znapzend:tsformat       %Y-%m-%d-%H%M%S${srcsuffix}
${prefix}org.znapzend:enabled        ${srcenabled}${srcenabledsuffix}
${prefix}org.znapzend:dst_0          backup/${dstname}${srcsuffix}
${prefix}org.znapzend:dst_a          root\@remote:remote/${dstname}${srcsuffix}
${prefix}org.znapzend:dst_fail       backup/destfail${srcsuffix}
${prefix}org.znapzend:mbuffer        $FindBin::Bin/mbuffer:31337${srcsuffix}
${prefix}org.znapzend:mbuffer_size   100M${srcsuffix}
${prefix}org.znapzend:pre_znap_cmd   /bin/echo 'pre znap command'${srcsuffix}
${prefix}org.znapzend:post_znap_cmd  /bin/echo 'post znap command'${srcsuffix}
ZFS_GET_END

        if ( defined($ENV{'ZNAPZENDTEST_ZFS_GET_ZEND_DELAY'}) ) {
            print <<"ZFS_GET_END";
${prefix}org.znapzend:zend_delay     $ENV{'ZNAPZENDTEST_ZFS_GET_ZEND_DELAY'}${srcsuffix}
ZFS_GET_END
        }

    if (defined ($dataSets{"backup/${dstname}${srcsuffix}"}) && $dataSets{"backup/${dstname}${srcsuffix}"} eq 'dst-disabled') {
            print <<"ZFS_GET_END";
${prefix}org.znapzend:dst_0_enabled  off
ZFS_GET_END
    }

} # end of print_zfs_get()

#print zfs command
print STDERR '# zfs ' . join(' ', @ARGV) . "\n";

my $command = shift @ARGV or exit 1;

for ($command){
    /^(?:set|inherit|send|recv|create)$/ && do {
        if ( defined($ENV{"ZNAPZENDTEST_ZFS_FAIL_$command"}) ) {
            exit 1;
        };
        if ( defined($ENV{"ZNAPZENDTEST_ZFS_SUCCEED_$command"}) ) {
            exit 0;
        };
        exit;
    };

    /^snapshot$/ && do {
        if ( defined($ENV{"ZNAPZENDTEST_ZFS_FAIL_$command"}) ) {
            exit 1;
        };
        if ( defined($ENV{"ZNAPZENDTEST_ZFS_SUCCEED_$command"}) ) {
            exit 0;
        };
        exit 1; # (Legacy) default for tests around this: pretend snapshot to fail => snapshot does exist already
    };

    /^list$/ && do {
        if ( defined($ENV{'ZNAPZENDTEST_ZFS_FAIL_list'}) ) {
            print STDERR "failed to list mock dataset(s)\n";
            exit 1;
        }
        if ($ARGV[4] && $ARGV[3] eq '-t' && $ARGV[4] eq 'snapshot'){
            if ($ARGV[-1] =~ /^[^\@]+\@[^\@]+$/){
                print $ARGV[-1] . "\n";
                exit;
            }
            exit 1 if !exists $dataSets{$ARGV[-1]};
            my $snapCount = ($dataSets{$ARGV[-1]} eq 'src') ? 60 : 58;
            #get timestamp rounded to minutes
            my $time = localtime(int(time / 60) * 60 - 3600);
            for (my $i = 0; $i < $snapCount; $i++){
                print $ARGV[-1] . '@' . $time->strftime('%Y-%m-%d-%H%M%S') . "\n";
                $time += 60;
            }
            exit;
        }
        # If a dataset name was passed on CLI, show (or recurse) it
        if (defined($ARGV[-1])) {
            if (exists $dataSetsMissing{$ARGV[-1]}) {
                print STDERR "cannot open '$ARGV[-1]': dataset does not exist\n";
                exit 1;
            }
            if (exists $dataSets{$ARGV[-1]}) {
                my $srcds = $ARGV[-1];
                if (join(" ", @ARGV) =~ / -r /) {
                    for my $childds (sort keys %dataSets){
                        # Here a "child" concept includes the specified
                        # dataset too, unlike "zfs get" logic below
                        if ( $childds =~ /^$srcds(\/|$)/ ) {
                            print "$childds\n";
                        }
                    }
                } else {
                    print "$srcds\n" if grep { $srcds eq $_ } keys %dataSets;
                }
                exit;
            }
        }
        for (sort keys %dataSets){
            print "$_\n";
        }
        exit;
    };

    /^destroy$/ && do {
        if ($ARGV[0] eq '-nv'){
            print "would reclaim 1337G\n";
        }
        if ( defined($ENV{'ZNAPZENDTEST_ZFS_FAIL_destroy'}) ) {
            print STDERR "failed to destroy mock dataset(s)\n";
            exit 1;
        }
        exit;
    };

    /^get$/ && do {
        #if ($ARGV[6] && $ARGV[6] eq 'usedbysnapshots'){
        if ( join(" ", @ARGV) =~ /usedbysnapshots/ ){
            print "10G\n";
            exit;
        }

#print STDERR "=== zfs : recursive=" . (join(" ", @ARGV) =~ / -r / ? "Y" : "N")
#    . " ARG='$ARGV[-1]' tabledata='$dataSets{$ARGV[-1]}'\n" ;

        if (defined($ARGV[-1]) && exists $dataSetsMissing{$ARGV[-1]}) {
            print STDERR "cannot open '$ARGV[-1]': dataset does not exist\n";
            exit 1;
        }

        # Note: illumos zfs as of 2019 and some years before allows
        # `zfs get -t filesystem,volume ...` (and can list snapshot
        # props otherwise) but Sol 10 did not
        if ( (join(" ", @ARGV) =~ / -t / ) && (defined $ENV{'ZNAPZENDTEST_ZFS_GET_TYPE_UNHANDLED'}) ) {
            # Report like Solaris 10 did
            print STDERR "invalid option 't'
usage:
        get [-rHp] [-d max] [-o \"all\" | field[,...]] [-s source[,...]]
            <\"all\" | property[,...]> [filesystem|volume|snapshot] ...
\n";
            exit 2;
        }

        # Note: In this line and in selections below we explicitly
        # but somewhat artificially rule out displaying datasets
        # under "backup" that are a pool root and destinations.
        # Real "zfs get" would show these.
        exit if !( (defined($ARGV[-1]) && exists $dataSets{$ARGV[-1]} && ($dataSets{$ARGV[-1]} =~ /^src/)) || (join(" ", @ARGV) =~ / -r /) );

        my $localattr = (join(" ", @ARGV) =~ / -s local/);
        my $inheritedattr = (join(" ", @ARGV) =~ / -s (local,|)inherited/);

        my $prefix = "";
        my $srcds = "";
        my $srcdsOrig = "";
        my $printSource = 0;
        if ( join(" ", @ARGV) =~ /name,property,value,source/ ) {
            $printSource = 1;
        }

        if (defined($ARGV[-1]) && exists $dataSets{$ARGV[-1]}) {
            $srcds = "$ARGV[-1]";
            $srcdsOrig = $srcds;
        } else {
            $srcds = "tank";
        }

        if ( join(" ", @ARGV) =~ /name,property,value/ ) {
            # The request we do when recursively looking for dataset props
            # Asked to print a prefix
            $prefix = "$srcds";
        }

        # Print a tree if either recursing from given srcds from CLI args,
        # or have none given at all (and so printing the world). Note that
        # the dataset in CLI args *must* be one from the array, otherwise
        # the mockup treats it as a random `zfs` option and ignores it.
        if (
            (join(" ", @ARGV) =~ / -r / && $srcds ne '')
         || ($srcdsOrig eq '')
        ) {
#print STDERR "=== zfs get : recursive=" . (join(" ", @ARGV) =~ / -r / ? "Y" : "N")
#    . " srcds='$srcds' srcdsOrig='$srcdsOrig' prefix='$prefix'\n" ;

            # Note that listing `zfs get -r -o property,value all dataset`
            # with both this code and real ZFS returns a lot of repetitive
            # lines (same keys, different values) for dataset's children
            # that you can't really tell apart without the "name" column.

            # Per checks above we know the $prefix is empty or exactly
            # last ARGV item.

            if (   (!$localattr && !$inheritedattr && $dataSets{$srcds} =~ /^src/)
                || ($localattr && $dataSets{$srcds} eq 'src')
                || ($inheritedattr && $dataSets{$srcds} eq 'src-inherited')
            ) {
                my $dstds;
                if ($srcds =~ /^tank\/source($|\/.*$)/ ) {
                    $dstds = "destination$1";
                } else {
                    $dstds = $srcds;
                }
                print_zfs_get ($prefix, $srcds, $dstds, $printSource);
            }

            # Does the srcds (from prefix or defaulted) have any descendants?
            for my $childds (sort(keys %dataSets)) {
                if ( $childds =~ /^$srcds\// ) {
                    my $dstds;
                    if ($childds =~ /^tank\/source($|\/.*$)/ ) {
                        $dstds = "destination$1";
                    } else {
                        $dstds = $childds;
                    }
                    if (   (!$localattr && !$inheritedattr && $dataSets{$childds} =~ /^src/)
                        || ($localattr && $dataSets{$childds} eq 'src')
                        || ($inheritedattr && $dataSets{$childds} eq 'src-inherited')
                    ) {
                        print_zfs_get ( ($prefix eq '') ? '' : $childds, $childds, $dstds, $printSource);
                    }
                }
            }
        } else {
            if (   (!$localattr && !$inheritedattr && $dataSets{$srcds} =~ /^src/)
                || ($localattr && $dataSets{$srcds} eq 'src')
                || ($inheritedattr && $dataSets{$srcds} eq 'src-inherited')
            ) {
                my $dstds;
                if ($srcds =~ /^tank\/source($|\/.*$)/ ) {
                    $dstds = "destination$1";
                } else {
                    $dstds = $srcds;
                }
                print_zfs_get ($prefix, $srcds, $dstds, $printSource);
            }
        }
        exit;
    };

    exit 1;
}

1;
